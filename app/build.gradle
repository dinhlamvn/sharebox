plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'

    // Google service plugin
    id 'com.google.gms.google-services'

    id 'com.google.firebase.crashlytics'

    id 'kotlin-parcelize'
}

def localProperties = new Properties()
if (rootProject.file("local.properties").exists()) {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.dinhlam.sharebox"
        minSdk 21
        targetSdk 33
        versionCode 202307071
        versionName "3.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProviders(
                        new RoomSchemaArgProvider(new File(projectDir, "schemas"))
                )
            }
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("store/key")
            storePassword localProperties["KSTOREPWD"]
            keyAlias localProperties["KSTOREALS"]
            keyPassword localProperties["KEYPWD"]
        }
    }

    sourceSets {
        dev {
            res.srcDirs = ['src/dev/res']
        }
    }


    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            manifestPlaceholders = [
                    applicationName: "Sharebox Dev"
            ]
            dimension "version"
            applicationIdSuffix ".dev"
            buildConfigField "String", "FIREBASE_DATABASE_URL" , "\"https://sharebox-dev-502a2-default-rtdb.firebaseio.com\""
            buildConfigField "String", "FIREBASE_STORAGE_URL" , "\"gs://sharebox-dev-502a2.appspot.com\""
        }

        prod {
            manifestPlaceholders = [
                    applicationName: "Sharebox"
            ]
            dimension "version"
            buildConfigField "String", "FIREBASE_DATABASE_URL" , "\"https://share-box-6c293-default-rtdb.asia-southeast1.firebasedatabase.app\""
            buildConfigField "String", "FIREBASE_STORAGE_URL" , "\"gs://share-box-6c293.appspot.com\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    dynamicFeatures = []
    namespace 'com.dinhlam.sharebox'
}

dependencies {
    // Testing
    def junit_version = "4.13.2"
    testImplementation "junit:junit:$junit_version"

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    def androidx_core_ktx_version = "1.9.0"
    implementation "androidx.core:core-ktx:$androidx_core_ktx_version"

    def appcompat_version = "1.6.1"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    def material_version = "1.8.0"
    implementation "com.google.android.material:material:$material_version"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def ktx_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$ktx_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$ktx_version"
    //noinspection GradleDependency
    implementation "androidx.navigation:navigation-fragment-ktx:$ktx_version"
    //noinspection GradleDependency
    implementation "androidx.navigation:navigation-ui-ktx:$ktx_version"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'

    // Room
    def room_version = "2.5.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Hilt
    def hilt_version = "2.43.2"
    implementation("com.google.dagger:hilt-android:$hilt_version")
    implementation("androidx.hilt:hilt-common:1.0.0")
    implementation("androidx.hilt:hilt-work:1.0.0")
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")
    kapt("androidx.hilt:hilt-compiler:1.0.0")

    // Gson
    def gson_version = "2.9.1"
    implementation "com.google.code.gson:gson:$gson_version"

    // Webkit support
    def webkit_version = "1.7.0"
    implementation "androidx.webkit:webkit:$webkit_version"

    // Firebase service
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.firebaseui:firebase-ui-auth:7.2.0'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation "com.firebaseui:firebase-ui-storage:8.0.2"

    // Messaging
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Browser
    def browser_version = "1.5.0"
    implementation "androidx.browser:browser:$browser_version"

    // Jsoup for link preview
    def jsoup_version = "1.16.1"
    implementation "org.jsoup:jsoup:$jsoup_version"

    // Facebook shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // FontAwesome
    implementation "com.mikepenz:iconics-core:5.4.0"
    implementation "com.mikepenz:google-material-typeface:4.0.0.2-kotlin"
    implementation "com.mikepenz:fontawesome-typeface:5.9.0.2-kotlin@aar"

    // Worker manager
    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"
}

kapt {
    correctErrorTypes = true
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        return ["-Aroom.schemaLocation=${schemaDir.path}".toString()]
    }
}